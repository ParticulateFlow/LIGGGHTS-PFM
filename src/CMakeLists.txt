# ENABLE_PACKAGE: enable PACKAGE-modules (OFF by default)
# ENABLE_PACKAGEUSER: enable PACKAGEUSER-modules (OFF by default)
# SUFFIX: add sufix to binary-name (NONE by default)
# USERPATH: use RPATH for search of the shared library libliggghts, need to be switched on by packaging (ON by default)
# VERSION: version number (2.2 by default)
# LIB_SUFFIX: suffix, where libliggghts library will be installed, lib/${LIB_SUFFIX} (NONE by default)

PROJECT(LIGGGHTS C CXX)
CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

SET(LIGGGHTS_MAJOR_VERSION 3)
SET(LIGGGHTS_MINOR_VERSION 0)
SET(LIGGGHTS_PATCH_VERSION 3)
SET(LIGGGHTS_VERSION ${LIGGGHTS_MAJOR_VERSION}.${LIGGGHTS_MINOR_VERSION}.${LIGGGHTS_PATCH_VERSION})

IF (NOT CMAKE_CXX_FLAGS)
  #If flags are not set, add default flags
  SET(CMAKE_BUILD_TYPE Release)

  IF(CMAKE_COMPILER_IS_GNUCXX)
    SET(CMAKE_CXX_FLAGS "-Wall -fstack-protector --param=ssp-buffer-size=4 -Wformat -Wformat-security -Werror=format-security -Wno-maybe-uninitialized")
  ENDIF()
ENDIF()

FILE(GLOB SOURCES *.cpp)
LIST(REMOVE_ITEM SOURCES main.cpp)

IF(WIN32)
  INCLUDE_DIRECTORIES(WINDOWS/extra)
  ADD_DEFINITIONS(-D_USE_MATH_DEFINES -DNOMINMAX)

  IF(MSVC)
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /bigobj")
  ENDIF()

  FIND_PACKAGE(Cygwin REQUIRED)

  ADD_CUSTOM_TARGET(GenerateHeaders
           COMMAND GenerateHeaders.bat ${CYGWIN_INSTALL_PATH}
           WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/WINDOWS)
ELSE()
  ADD_CUSTOM_TARGET(GenerateHeaders
           COMMAND sh Make.sh style
           COMMAND sh Make.sh models
           WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR})
ENDIF()

ADD_LIBRARY(liggghts SHARED ${SOURCES})
ADD_DEPENDENCIES(liggghts GenerateHeaders)

ADD_EXECUTABLE(liggghts_bin main.cpp)
TARGET_LINK_LIBRARIES(liggghts_bin liggghts)
SET_TARGET_PROPERTIES(liggghts_bin PROPERTIES OUTPUT_NAME liggghts)

#=======================================

FIND_PACKAGE(VTK COMPONENTS Common Filtering IO)

IF(VTK_FOUND)
  INCLUDE(${VTK_USE_FILE})
  ADD_DEFINITIONS(-DLAMMPS_VTK)
  TARGET_LINK_LIBRARIES(liggghts ${VTK_LIBRARIES})
  MESSAGE(STATUS "Found VTK")
ELSE(VTK_FOUND)
  MESSAGE(STATUS "VTK NOT found!")
ENDIF(VTK_FOUND)

#=======================================

FIND_PACKAGE(JPEG)

IF(JPEG_FOUND)
  INCLUDE_DIRECTORIES(${JPEG_INCLUDE_DIR})
  ADD_DEFINITIONS(-DLAMMPS_JPEG)
  TARGET_LINK_LIBRARIES(liggghts jpeg)
ELSE(JPEG_FOUND)
  MESSAGE(STATUS "JPEG NOT found!")
ENDIF(JPEG_FOUND)

#=======================================

FIND_PACKAGE(MPI)

IF(MPI_FOUND)
  INCLUDE_DIRECTORIES(${MPI_INCLUDE_PATH})
  TARGET_LINK_LIBRARIES(liggghts ${MPI_LIBRARIES})

  IF(MPI_COMPILE_FLAGS)
    SET_TARGET_PROPERTIES(liggghts PROPERTIES COMPILE_FLAGS "${MPI_COMPILE_FLAGS}")
  ENDIF()

  IF(MPI_LINK_FLAGS)
    SET_TARGET_PROPERTIES(liggghts PROPERTIES LINK_FLAGS "${MPI_LINK_FLAGS}")
  ENDIF()
ELSE(MPI_FOUND)
  MESSAGE(STATUS "MPI NOT found! Using MPI Stubs instead.")
  ADD_SUBDIRECTORY(STUBS)
  TARGET_LINK_LIBRARIES(liggghts STUBS)
ENDIF(MPI_FOUND)

#=======================================

#Disabled modules
#GPU KIM MEAM POEMS REAX SRD
IF(ENABLE_PACKAGE)
  SET(MODULES_PACKAGE ASPHERE CLASS2 COLLOID DIPOLE FLD  GRANULAR KSPACE MANYBODY MC MOLECULE OPT PERI REPLICA SHOCK  XTC)
ENDIF(ENABLE_PACKAGE)
#Disabled modules
#USER-MISC
IF(ENABLE_PACKAGEUSER)
  SET(MODULES_USER USER-ATC USER-AWPMD USER-CG-CMM USER-CUDA USER-EFF USER-EWALDN USER-OMP USER-REAXC USER-SPH)
ENDIF(ENABLE_PACKAGEUSER)
#=======================================

enable_testing()
include_directories(${CMAKE_CURRENT_SOURCE_DIR})
add_subdirectory(tests)
